#! /usr/bin/env zsh

# Soundswitcher
# Switch sound devices.

function configure_jack {
  # set driver (ds — driver set); get a drivers list by `jack_control dl`
  jack_control ds alsa

  # set driver parameters (dps — driver parameter set); get a driver parameters list by `jack_control dp`
  # set ALSA device name
  case $1 {
    (bluealsa) jack_control dps device bluealsa ;;

    (usb) jack_control dps device hw:2 ;;
  }

  # set the sample rate
  jack_control dps rate 48000

  # set the frames per period between process( ) calls
  # also known as `buffer size`
  jack_control dps period 1024

  # set the number of periods of playback latency
  jack_control dps nperiods 2
}

function start_jack {
  jack_control start
}

function stop_jack {
  jack_control stop
}

function set_alsa {
  case $1 {
    (internal)
cat <<HEREDOC > ~/.asoundrc
# device: internal

defaults.pcm.card 0
defaults.ctl.card 0
HEREDOC
    ;;

    (usb)
cat <<HEREDOC > ~/.asoundrc
# device: usb

defaults.pcm.card 2
defaults.ctl.card 2
HEREDOC
    ;;

    (bluealsa)
cat <<HEREDOC > ~/.asoundrc
# device: bluealsa

defaults.bluealsa.service "org.bluealsa"
defaults.bluealsa.profile "a2dp"
defaults.bluealsa.device  "40:ED:98:19:9C:33"
defaults.bluealsa.delay   10000
defaults.bluealsa.battery "yes"

pcm.!default {
 type plug
 slave.pcm "bluealsa"
}
HEREDOC
    ;;
  }
}

function set_polybar {
  case $1 {
    (internal)
cat <<HEREDOC > ~/.config/polybar/soundcard.conf
master-soundcard = hw:0
master-mixer     = Master

format-volume-prefix = int vol
format-muted-prefix  = int vol
HEREDOC
    ;;

    (usb_alsa)
cat <<HEREDOC > ~/.config/polybar/soundcard.conf
master-soundcard = hw:2
master-mixer     = PCM

format-volume-prefix = usb a vol
format-muted-prefix  = usb a vol
HEREDOC
    ;;

    (usb_jack)
cat <<HEREDOC > ~/.config/polybar/soundcard.conf
master-soundcard = hw:2
master-mixer     = PCM

format-volume-prefix = usb j vol
format-muted-prefix  = usb j vol
HEREDOC
    ;;

    (bluealsa_alsa)
cat <<HEREDOC > ~/.config/polybar/soundcard.conf
master-soundcard = bluealsa
master-mixer     = FiiO BTR3 - A2DP

format-volume-prefix = bt a vol
format-muted-prefix  = bt a vol
HEREDOC
    ;;

    (bluealsa_jack)
cat <<HEREDOC > ~/.config/polybar/soundcard.conf
master-soundcard = bluealsa
master-mixer     = FiiO BTR3 - A2DP

format-volume-prefix = bt j vol
format-muted-prefix  = bt j vol
HEREDOC
    ;;
  }
}

function restart_polybar {
  polybar-msg cmd restart
}

function set_goldendict {
  case $1 {
    (alsa) sed --in-place 's/mplayer -ao jack/mplayer -ao alsa/' ~/.goldendict/config ;;

    (jack) sed --in-place 's/mplayer -ao alsa/mplayer -ao jack/' ~/.goldendict/config ;;
  }
}

function is_internal_exist {
  cat /proc/asound/cards | [[ -n $(grep '0 \[PCH') ]]
}

function is_usb_exist {
  cat /proc/asound/cards | [[ -n $(grep '2 \[BTR3') ]]
}

function is_bluealsa_exist {
  bluetoothctl info '40:ED:98:19:9C:33' | [[ $(grep 'Connected:' | cut -d ' ' -f2) == 'yes' ]]
}

function switch_internal {
  if is_internal_exist
  then
    stop_jack
    set_alsa internal
    set_polybar internal
    restart_polybar
    set_goldendict alsa
  fi
}

function switch_usb_alsa {
  if is_usb_exist
  then
    stop_jack
    set_alsa usb
    set_polybar usb_alsa
    restart_polybar
    set_goldendict alsa
  fi
}

function switch_usb_jack {
  if is_usb_exist
  then
    stop_jack
    set_alsa usb
    configure_jack usb
    start_jack
    set_polybar usb_jack
    restart_polybar
    set_goldendict jack
  fi
}

function switch_bluealsa_alsa {
  if is_bluealsa_exist
  then
    stop_jack
    set_alsa bluealsa
    set_polybar bluealsa_alsa
    restart_polybar
    set_goldendict alsa
  fi
}

function switch_bluealsa_jack {
  if is_bluealsa_exist
  then
    stop_jack
    set_alsa bluealsa
    configure_jack bluealsa
    start_jack
    set_polybar bluealsa_jack
    restart_polybar
    set_goldendict jack
  fi
}

function main {
  case $1 {
    (switch_internal) switch_internal ;;

    (switch_usb_alsa) switch_usb_alsa ;;

    (switch_usb_jack) switch_usb_jack ;;

    (switch_bluealsa_alsa) switch_bluealsa_alsa ;;

    (switch_bluealsa_jack) switch_bluealsa_jack ;;
  }
}

main $1

# vim: set textwidth=80 colorcolumn=80:
